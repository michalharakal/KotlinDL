project.setDescription("This module contains the Kotlin API implemetantion code for building, training, and evaluating the Deep Learning models.")

apply plugin: 'org.jetbrains.kotlin.multiplatform'
apply plugin: 'com.android.library'

kotlin {
    jvmToolchain(21)

    jvm {
        testRuns["test"].executionTask.configure {
            useJUnitPlatform()
        }
    }
    androidTarget {
        publishLibraryVariants("release")
    }
    sourceSets {
        commonMain {
            dependencies {
                api project(':api')
                // SKaiNET core dependencies
                api 'sk.ainet.core:skainet-lang-core:0.7.1'
                api 'sk.ainet.core:skainet-backend-cpu:0.7.1'
                api 'sk.ainet.core:skainet-compile-dag:0.7.1'
            }
        }
        jvmMain {
            dependencies {
                api 'com.github.doyaaaaaken:kotlin-csv-jvm:0.7.3' // for csv parsing
                api 'io.github.microutils:kotlin-logging:2.1.21' // for logging
                api 'io.jhdf:jhdf:0.5.7' // for hdf5 parsing
                api 'com.beust:klaxon:5.5'
                implementation 'com.twelvemonkeys.imageio:imageio-jpeg:3.8.2'
            }
        }
        jvmTest {
            dependencies {
                implementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
                implementation 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
                implementation 'org.junit.jupiter:junit-jupiter-params:5.8.2'
            }
        }
        androidMain {
            dependencies {
                api 'androidx.camera:camera-core:1.0.0-rc03'
            }
        }
    }
    explicitApiWarning()
}

android {
    compileSdk 34
    namespace = 'org.jetbrains.kotlinx.dl.impl'
    defaultConfig {
        minSdk 26
        targetSdk 34
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_21
        targetCompatibility JavaVersion.VERSION_21
    }
}