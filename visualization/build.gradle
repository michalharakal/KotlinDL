project.setDescription("This module contains the Kotlin API for visualization of the Deep Learning models built with the KotlinDL.")

apply plugin: 'org.jetbrains.kotlin.multiplatform'
apply plugin: 'com.android.library'

kotlin {
    jvmToolchain(21)

    jvm()
    androidTarget {
        publishLibraryVariants("release")
    }
    sourceSets {
        jvmMain {
            dependencies {
                api project(":api")
                api project(":tensorflow")

                def excludeSLF4JImpl = { exclude group: 'org.slf4j', module: 'slf4j-simple' }

                api 'org.jetbrains.lets-plot:lets-plot-batik:4.0.1', excludeSLF4JImpl
                api 'org.jetbrains.lets-plot:lets-plot-common:4.0.1', excludeSLF4JImpl
                api 'org.jetbrains.lets-plot:lets-plot-kotlin-jvm:4.4.3', excludeSLF4JImpl
            }
        }
        androidMain {
            dependencies {
                api project(":api")
                api "androidx.camera:camera-view:1.0.0-alpha22"
            }
        }
    }
}

android {
    compileSdk 34
    namespace = 'org.jetbrains.kotlinx.dl.visualization'
    defaultConfig {
        minSdk 26
        targetSdk 34
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_21
        targetCompatibility JavaVersion.VERSION_21
    }
}
